<!DOCTYPE html>
<html lang="en">
	<head>
		<title>Perlin noise - DPWOERT.COM</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<link rel="stylesheet" type="text/css" href="style.css"></link>

		<!-- THREE -->
		<script src="../../bower_components/threejs/build/three.min.js"></script>
		<script src="OrbitControls.js"></script>

		<!-- render pipeline library -->
		<script src="../../src/shader-step.js"></script>
		<script src="../../src/render-pipeline.js"></script>
		<script src="../../src/process-step.js"></script>

		<!-- demo javascript -->
		<script src="demo.js"></script>

		<!-- shader - simple vertex shader -->
		<script id="simpleVertex" type="x-shader/x-vertex">

			varying vec2 vUv;

			void main() {

				vUv = uv;
				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

			}

		</script>

		<!-- shader - copy/save to screen -->
		<script id="copyFragment" type="x-shader/x-vertex">

			uniform sampler2D texData;
			varying vec2 vUv;

			void main() {

				vec4 texel = texture2D( texData, vUv );
				gl_FragColor = 1.0 * texel;

			}


		</script>

		<!-- shader - game of life rules -->
		<script id="perlinFragment" type="x-shader/x-fragment">

			uniform sampler2D texData;
			varying vec2 vUv;

			uniform vec2 u_resolution;
			uniform float random;
			uniform float scale;

			//
			// Description : Array and textureless GLSL 2D simplex noise function.
			//      Author : Ian McEwan, Ashima Arts.
			//  Maintainer : ijm
			//     Lastmod : 20110822 (ijm)
			//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
			//               Distributed under the MIT License. See LICENSE file.
			//               https://github.com/ashima/webgl-noise
			//

			vec3 mod289(vec3 x) {
				return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec2 mod289(vec2 x) {
				return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec3 permute(vec3 x) {
				return mod289(((x*34.0)+1.0)*x);
			}

			float snoise(vec2 v){

				const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
									0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
									-0.577350269189626,  // -1.0 + 2.0 * C.x
									0.024390243902439); // 1.0 / 41.0

				// First corner
				vec2 i  = floor(v + dot(v, C.yy) );
				vec2 x0 = v -   i + dot(i, C.xx);

				// Other corners
				vec2 i1;
				//i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
				//i1.y = 1.0 - i1.x;
				i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
				// x0 = x0 - 0.0 + 0.0 * C.xx ;
				// x1 = x0 - i1 + 1.0 * C.xx ;
				// x2 = x0 - 1.0 + 2.0 * C.xx ;
				vec4 x12 = x0.xyxy + C.xxzz;
				x12.xy -= i1;

				// Permutations
				i = mod289(i); // Avoid truncation effects in permutation
				vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))+ i.x + vec3(0.0, i1.x, 1.0 ));

				vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
				m = m*m ;
				m = m*m ;

				// Gradients: 41 points uniformly over a line, mapped onto a diamond.
				// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

				vec3 x = 2.0 * fract(p * C.www) - 1.0;
				vec3 h = abs(x) - 0.5;
				vec3 ox = floor(x + 0.5);
				vec3 a0 = x - ox;

				// Normalise gradients implicitly by scaling m
				// Approximation of: m *= inversesqrt( a0*a0 + h*h );
				m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

				// Compute final noise value at P
				vec3 g;
				g.x  = a0.x  * x0.x  + h.x  * x0.y;
				g.yz = a0.yz * x12.xz + h.yz * x12.yw;
				return 130.0 * dot(m, g);

			}

			void main() {

				float n = snoise( vUv * scale + (scale*random) );
				float n2 = snoise( vUv * scale );
  				gl_FragColor = vec4(0.5 + 0.5 * n, 0.5 + 0.5 * n2, 0.5 + 0.5 * n2, 1.0);

			}

		</script>

		<script id="velocityFragment" type="x-shader/x-fragment">

			uniform sampler2D texData;
			uniform sampler2D texPerlin;
			uniform sampler2D texPos;
			uniform float initial;

			varying vec2 vUv;

			void main() {

				vec3 current = texture2D( texData, vUv ).xyz;
				vec3 pos = texture2D( texPos, vUv ).xyz;
				vec3 force = texture2D( texPerlin, pos.xy ).xyz;

				vec3 applied = current - 0.5;

				//apply drag
				applied = applied * 0.8;

				//force to real word values
				force = (force - 0.5) * 0.075;

				//apply force
				applied = applied + force;

				//drag
				applied *= 0.7;

				//transform back to float
				applied = applied + 0.5;

				applied = clamp(applied, 0.0, 1.0);

				//save force
				gl_FragColor = vec4(applied, 1.0);

			}

		</script>

		<script id="positionFragment" type="x-shader/x-fragment">

			uniform sampler2D texData;
			uniform sampler2D texVelocity;
			uniform sampler2D texPerlin;
			uniform vec2 u_resolution;
			uniform float u_time;
			uniform float speed;

			varying vec2 vUv;

			void main() {

				vec4 current = texture2D( texData, vUv );
				vec3 velocity = texture2D( texVelocity, current.xy ).xyz;


				// velocity = velocity * 2.0 - 1.0;
				velocity = velocity - 0.5;

				//apply scaling to velocities
				velocity = velocity * 0.0075;

				//apply velocity
				vec4 newPos = current + vec4(velocity, 1.0);

				//reborn when dead
				if( (distance(current, newPos) < 0.001) && u_time > 10.0 ){
					newPos = texture2D( texPerlin, vUv ).zxyw;
				}

				//die when crossing edges
				if( newPos.x >= 1.0 || newPos.x <= 0.0 || newPos.y >= 1.0 || newPos.y <= 0.0 || newPos.z <= 0.0 || newPos.z >= 1.0 ){
					newPos = texture2D( texPerlin, vUv ).zxyw;
				}

				gl_FragColor = newPos;

			}

		</script>

		<script id="renderVertex" type="x-shader/x-vertex">

			uniform sampler2D texPosition;
			uniform vec2 u_resolution;

			void main() {

				vec3 newPos = texture2D(texPosition, position.xy).xyz;

				// newPos.z = newPos.z - 0.5;
				// vec4 transPos = newPos * 512.0 - 256.0;
				vec3 transPos = newPos;
				transPos.x = transPos.x * 2.0 - 1.0;
				transPos.y = transPos.y * 2.0 - 1.0;
				transPos.z = transPos.z * 2.0 - 1.0;

				transPos.xy *= 50.0;
				transPos.z *= 50.0;

				gl_Position = projectionMatrix * modelViewMatrix * vec4( transPos, 1.0 );

				float size = 20.0;
				float scale = 2.0;

				vec4 mvPosition = modelViewMatrix * vec4( transPos, 1.0 );
				gl_PointSize = size * ( scale / length( mvPosition.xyz ) );

			}

		</script>

		<script id="renderFragment" type="x-shader/x-vertex">

			uniform sampler2D texPosition;

			void main() {

				vec2 coord = gl_PointCoord - vec2(0.5);  //from [0,1] to [-0.5,0.5]
				float alpha = length(coord);

				//make circle
				if(alpha > 0.5)
					discard;

				// gl_FragColor = mapping;
				gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0 - alpha);

			}

		</script>

	</head>

	<body>

		<div id="canvas"></div>

	</body>

</html>
