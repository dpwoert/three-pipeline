<!DOCTYPE html>
<html lang="en">
	<head>
		<title>Perlin noise - DPWOERT.COM</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<link rel="stylesheet" type="text/css" href="style.css"></link>

		<!-- THREE -->
		<script src="../../bower_components/threejs/build/three.min.js"></script>

		<!-- render pipeline library -->
		<script src="../../src/shader-step.js"></script>
		<script src="../../src/render-pipeline.js"></script>

		<!-- demo javascript -->
		<script src="demo.js"></script>

		<!-- shader - simple vertex shader -->
		<script id="simpleVertex" type="x-shader/x-vertex">

			varying vec2 vUv;

			void main() {

				vUv = uv;
				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

			}

		</script>

		<!-- shader - copy/save to screen -->
		<script id="copyFragment" type="x-shader/x-vertex">

			uniform sampler2D texData;
			varying vec2 vUv;

			void main() {

				vec4 texel = texture2D( texData, vUv );
				gl_FragColor = 1.0 * texel;

			}


		</script>

		<!-- shader - game of life rules -->
		<script id="perlinFragment" type="x-shader/x-fragment">

			uniform sampler2D texData;
			varying vec2 vUv;

			uniform vec2 u_resolution;
			uniform float random;

			//
			// Description : Array and textureless GLSL 2D simplex noise function.
			//      Author : Ian McEwan, Ashima Arts.
			//  Maintainer : ijm
			//     Lastmod : 20110822 (ijm)
			//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
			//               Distributed under the MIT License. See LICENSE file.
			//               https://github.com/ashima/webgl-noise
			//

			vec3 mod289(vec3 x) {
				return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec2 mod289(vec2 x) {
				return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec3 permute(vec3 x) {
				return mod289(((x*34.0)+1.0)*x);
			}

			float snoise(vec2 v){

				const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
									0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
									-0.577350269189626,  // -1.0 + 2.0 * C.x
									0.024390243902439); // 1.0 / 41.0

				// First corner
				vec2 i  = floor(v + dot(v, C.yy) );
				vec2 x0 = v -   i + dot(i, C.xx);

				// Other corners
				vec2 i1;
				//i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
				//i1.y = 1.0 - i1.x;
				i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
				// x0 = x0 - 0.0 + 0.0 * C.xx ;
				// x1 = x0 - i1 + 1.0 * C.xx ;
				// x2 = x0 - 1.0 + 2.0 * C.xx ;
				vec4 x12 = x0.xyxy + C.xxzz;
				x12.xy -= i1;

				// Permutations
				i = mod289(i); // Avoid truncation effects in permutation
				vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))+ i.x + vec3(0.0, i1.x, 1.0 ));

				vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
				m = m*m ;
				m = m*m ;

				// Gradients: 41 points uniformly over a line, mapped onto a diamond.
				// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

				vec3 x = 2.0 * fract(p * C.www) - 1.0;
				vec3 h = abs(x) - 0.5;
				vec3 ox = floor(x + 0.5);
				vec3 a0 = x - ox;

				// Normalise gradients implicitly by scaling m
				// Approximation of: m *= inversesqrt( a0*a0 + h*h );
				m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

				// Compute final noise value at P
				vec3 g;
				g.x  = a0.x  * x0.x  + h.x  * x0.y;
				g.yz = a0.yz * x12.xz + h.yz * x12.yw;
				return 130.0 * dot(m, g);

			}

			void main() {

				float n = snoise( vUv * 12.0 + (12.0*random) );
				float n2 = snoise( vUv * 12.0 );
  				gl_FragColor = vec4(0.5 + 0.5 * n, 0.5 + 0.5 * n2, 0.0, 1.0);

			}

		</script>

		<script id="velocityFragment" type="x-shader/x-fragment">

			uniform sampler2D texData;
			uniform sampler2D texPerlin;
			uniform sampler2D texPos;
			varying vec2 vUv;

			void main() {

				vec2 current = texture2D( texData, vUv ).xy;
				vec2 pos = texture2D( texPos, vUv ).xy;
				vec2 force = texture2D( texPerlin, pos ).xy;

				vec2 applied = current - 0.5;

				//apply drag
				applied = applied * 0.9;

				//force to real word values
				force = (force - 0.5) * 0.5;

				//apply force
				applied = applied + force;
				applied = applied + 0.5;

				//save force
				gl_FragColor = vec4(applied, 0.0, 1.0);

			}

		</script>

		<script id="positionFragment" type="x-shader/x-fragment">

			uniform sampler2D texData;
			uniform sampler2D texVelocity;
			varying vec2 vUv;

			uniform vec2 u_resolution;

			void main() {

				vec4 current = texture2D( texData, vUv );
				vec2 velocity = texture2D( texVelocity, current.xy ).xy;

				// velocity = velocity * 2.0 - 1.0;
				velocity = (velocity - 0.5) * 0.1;

				//apply velocity
				vec4 newPos = current + vec4(velocity, 0.0, 0.0);

				//reborn when dead
				if(length(velocity) < 0.001){
					newPos = vec4(0.5, 0.5, 0.0, 1.0);
				}

				gl_FragColor = newPos;

			}

		</script>

		<script id="renderVertex" type="x-shader/x-vertex">

			uniform sampler2D texPosition;
			uniform vec2 u_resolution;

			varying vec2 v_pos;

			void main() {

				vec4 newPos = texture2D(texPosition, position.xy);

				// gl_Position = projectionMatrix * modelViewMatrix * newPos;
				gl_Position = newPos * 512.0 - 256.0;
				gl_PointSize = 1.0;

				v_pos = gl_Position.xy;

			}

		</script>

		<script id="renderFragment" type="x-shader/x-vertex">

			uniform sampler2D texPosition;
			uniform sampler2D texVelocity;

			varying vec2 v_pos;

			void main() {

				vec4 color = texture2D( texVelocity, v_pos / 512.0 );
				gl_FragColor = vec4(0.0, color.x, color.y, 1.0);

			}

		</script>

	</head>

	<body>

		<div id="canvas"></div>

	</body>

</html>
